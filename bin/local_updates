#!/usr/bin/env perl

use strict;
use warnings;
use lib 'lib';

use Jifty;
BEGIN { Jifty->new; }
use Benchmark qw/timestr/;

use Gearman::Worker;
my $worker = Gearman::Worker->new(
    job_servers => Jifty->config->app('job_servers'),
);

# User task, synchronous
$worker->register_function(
    check_queue => sub {
        my $restarted = Smokingit->check_queue;
        return "Restarted $restarted tasks\n";
    },
);

# Synchronous task, from worker
$worker->register_function(
    post_results => sub {
        my $job = shift;
        my $smoke = Smokingit::Model::SmokeResult->new;
        my ($ok, $msg) = $smoke->post_result($job->arg);
        warn "$msg\n";
        return $ok;
    },
);
$worker->register_function(
    sync_project => sub {
        my $job = shift;
        my $project_name = $job->arg;
        warn "Sync $project_name";

        my $project = Smokingit::Model::Project->new;
        $project->load_by_cols( name => $project_name );
        return "No such project: $project_name\n"
            unless $project->id;

        # Update or clone, as need be
        if (-d $project->repository_path) {
            warn "Updating $project_name\n";
            $project->update_repository;
        } else {
            warn "Cloning $project_name\n";
            system("git", "clone", "--quiet", "--mirror",
                   $project->repository_url,
                   $project->repository_path);
        }

        # Sync up the branches
        Jifty->handle->begin_transaction;
        my @results = $project->sync_branches;
        Jifty->handle->commit;

        return @results ? join("\n",@results,"") : "No changes\n";
    },
);
$worker->register_function(
    plan_tests => sub {
        my $job = shift;
        my $project_name = $job->arg;

        my $projects = Smokingit::Model::ProjectCollection->new;
        if ($project_name) {
            $projects->limit( column => "name", value => $project_name );
        } else {
            $projects->unlimit;
        }
        return 0 unless $projects->count;

        my $tests = 0;
        while (my $project = $projects->next) {
            Jifty->handle->begin_transaction;
            $tests += $project->schedule_tests;
            Jifty->handle->commit;
        }

        return $tests;
    }
);

# User task, synchronous
$worker->register_function(
    retest => sub {
        my $job = shift;
        my ($sha,$configname) = $job->arg =~ /^([0-9a-fA-F]+)(?:\s*\[(.*)\])?/;

        my $commits = Smokingit::Model::CommitCollection->new;
        $commits->limit( column => "sha", operator => "like", value => "$sha%" );
        return "Unknown SHA\n"   if $commits->count == 0;
        return "Ambiguous SHA\n" if $commits->count > 1;

        my @msgs;
        my $msg = sub { warn @_; push @msgs, @_ };

        my $commit = $commits->next;

        my $branch;

        my $configs = $commit->project->configurations;
        $configs->limit( column => "name", operator => "MATCHES", value => $configname )
            if defined $configname and length $configname;
        my $tests = 0;
        while (my $config = $configs->next) {
            my $summary = $commit->short_sha . "[" . $config->name ."]";
            my %lookup = (
                project_id       => $commit->project->id,
                configuration_id => $config->id,
                commit_id        => $commit->id,
            );
            my $smoke = Smokingit::Model::SmokeResult->new;
            $smoke->load_by_cols( %lookup );
            if ($smoke->id) {
                $msg->("Re-testing $summary\n");
                $smoke->set_submitted_at(undef);
                $smoke->set_gearman_process(undef);
                $tests += $smoke->run_smoke;
            } else {
                # Guess which branch
                unless ($branch) {
                    $branch = Smokingit::Model::Branch->new;
                    my @branches = $commit->branches;
                    $branch->load_by_cols( name => $branches[0], project_id => $commit->project->id)
                        if @branches == 1;
                }
                if ($branch->id) {
                    $msg->("Testing $summary\n");
                    $tests += $commit->run_smoke( $config, $branch );
                } else {
                    $msg->("No existing smoke for $summary found, and can't determine branch\n");
                }
            }
        }
        return join "", @msgs;
    },
);

$worker->work while 1;
